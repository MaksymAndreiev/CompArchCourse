;=========================== Програма обробки переривань таймера Т/С0 ==========================

IntTC0:	MOV	TH0,		#DTH		; Встановлення часу 60 мс у таймері T/C0
		MOV	TL0,		#DTL		; Встановлення часу 60 мс у таймері T/C0

;----------------------------- Перевірка того, чи пройшов час 0,36 с ---------------------------

		DJNZ	VTIM,		EXIT		; Якщо 0,36 с ще не пройшло, переходимо на вихід з 
							; переривання
		MOV	VTIM,		#006		; Інакше оновлюємо віртуальний таймер.

		CPL	FPART				; Зміна рухомої частини на протилежну
		JB	FMOVE,	MOVE		; Якщо встановлено режим руху, тоді перехід
		JNB	FMODE,	DISAPP	; Якщо режим зникнення, то перехід
		JNB	FPART,	HDST		; Якщо рухаємось головою, тоді перехід
		MOV	A,		TAIL		; \
		RLC	A				;  | Рухаємо хвіст черв'ячка
		MOV	TAIL,		A		; /
		SETB	FMOVE				; Встановлюємо флаг руху

EXIT:		RETI					; Вихід з переривання

HDST:		MOV	P1,		HEAD		; Рухаємо голову черв'ячка
		RETI					;

DISAPP:	JB	FPART,	TLDS		; Перехід, якщо рух хвостом
		MOV	A,		HEAD		; \
		RLC	A				;  | Якщо ні, рухаємося головою
		MOV	HEAD,		A		; /

		ORL	A,		TAIL		; Робимо черв'ячка з голови та хвоста

P3EX:		MOV	P3,		A		; Виводимо черв'ячка на порт P3
		CLR	C				; Очищуємо біт переносу С 
		RETI					;

TLDS:		MOV	A,		TAIL		; \
		RLC	A				;  | Рухаємося хвостом
		MOV	TAIL,		A		; /

		ORL	A,		HEAD		; Робимо черв'ячка з голови та хвоста
		LJMP	P3EX				;

MOVE:		MOV	A,		TAIL		; \
		JB	ACC.6,	PTMV		; / Перевірка чи знаходиться хвіст на 6 біті
		JB	FPART,	TLMV		; Якщо рухаємося хвостом, то перехід
		MOV	A,		HEAD		; \
		RL	A				;  | Рухаємо головою
		MOV	HEAD,		A		; /
		
		ORL	A,		TAIL		; Робимо черв'ячка з голови та хвоста
		JB	FPORT,	P3SV		; Якщо запис в P3, то перехід
		MOV	P1,		A		; Інакше, запис в P1
		RETI				      ;

P3SV:		MOV	P3,		A		; Запис в P3
		RETI					;

TLMV:		RLC	A				; \
		MOV	TAIL,		A		; / Переміщення хвоста
		 
		ORL	A,		HEAD		; Робимо черв'ячка з голови та хвоста 
		JB	FPORT,	P3ND		; Якщо запис в P3, то перехід
		MOV	P1,		A		; Інакше, запис в P1
		RETI

P3ND:		MOV	P3,		A		; Запис в P3
		MOV	P1,		#000		; Видалення змісту P1
		CLR	FMOVE				; Встановлення режиму появлення/зникнення
		RETI					;

PTMV:		JB	FPART,	TLND		; Якщо рухаємося хвостом, то перехід
		MOV	A,		HEAD		; \
		RL	A				;  | Рух головою
		MOV	HEAD,		A		; /

		MOV	P3,		A		; Записуємо голову в P3
		MOV	P1,		TAIL		; Записуємо хвіст в P1
		RETI

TLND:		RLC	A				; \
		MOV	TAIL,		A		;  | Рух хвостом
		MOV	P1,		A		; /
		JNB	ACC.7,	EXIT		; Перевірка чи знаходиться хвіст на останньому біті
		RLC	A				; \
		MOV	TAIL,		A		; / Рух хвостом
		CLR	FMODE				; Встановлення режиму зникнення
		SETB	FPORT				; Перехід запису на P1
		RETI					;