;=============================================================================
;      Програма побудови зображення прямокутника на екрані монітору
;-----------------------------------------------------------------------------
        USE16                   ; Генерувати 16-бітний код
        ORG     100h            ; Початок COM файлу
;-----------------------------------------------------------------------------
;___ Встановлення режиму VGA ___
        MOV     AX,      04F02h ;\ Очистити відеопам'ять та встановити
        MOV     BX,      00012h ;/ 12 відеорежим
        INT     010h            ; Викликаємо 10 переривання
;____Перехід до відеопам'яті____
        PUSH    0A000h          ;\
        POP     ES              ;/ Заносимо адресу, з якої починається
        ; відеопам'ять в сегментний регістр ES
;-----------------------------------------------------------------------------

        CALL    RECTANGLE       ; Викликаємо процедуру

;__Очікування натискання кнопки__
        MOV     AX,      00000h ;\
        INT     016h            ;/ Очікувати натискання наступної кнопки
;________________________________
        MOV     AX,      04C00h ;\
        INT     021h            ;/ Завершити роботу

;================ ПРОЦЕДУРА ПОБУДОВИ ЗОБРАЖЕННЯ ПРЯМОКУТНИКА =================
RECTANGLE:
;______SRR = 00Fh_______
        MOV     DX,      003CEh ; Встановлюємо адресу регістру графічного
		; контролера
        MOV     AL,      000h   ; Обираємо регістр встановлення/очищення SRR
        OUT     DX,      AL     ; Записуємо його індекс в індексний регістр DX
        INC     DX              ; Переходимо на адресу запису даних 03CFh
        MOV     AL,      00Fh   ; Готуємо слої до заповнення
        OUT     DX,      AL     ; Записуємо в регістр SRR
;______SRER = 001h______
        MOV     DX,      003CEh ; Встановлюємо адресу регістру графічного
		; контролера
        MOV     AL,      001h   ; Обираємо регістр дозволу встановлення/очищення 
		; SRЕR
        OUT     DX,      AL     ; Записуємо його індекс в індексний регістр DX
        INC     DX              ; Переходимо на адресу запису даних 03CFh
        MOV     AL,      001h   ; Готуємо слої до заповнення (синій колір)
        OUT     DX,      AL     ; Записуємо в регістр SRЕR
;_____Побудова прямокутника_____
        MOV     DI,      1602   ; Заносимо адресу першого байту нашого прямокутника
        MOV     AL,      000h   ; Задаємо графічний байт
        MOV     CX,      80     ; Встановлюю кількість рядків, які необхідно
        ; заповнити, щоб отримати необхідний прямокутник
RECT1:  XCHG    BX,      CX     ; Зберігаю значення регістра СХ та звільняю
		; для подальшої роботи
        MOV     CX,      10     ; Заношу в СХ кількість стовпців, які необхідно
        ; заповнити, щоб отримати одну лінію прямокутника
        REP STOSB               ; Зберігаємо AL за адресою ES:DI, доки СХ не 0
        ; а також зменшуємо його вміст
        ADD     DI,      070    ; Додаємо до адреси першого байту останні 70
        ; байт рядку, що не заповнені, щоб перейти на наступний рядок
        XCHG    BX,      CX     ; Повертаю збережене значення в СХ
        LOOP    RECT1           ; Доки СХ не 0, переходимо на мітку

        RET                     ; Вихід з процедури
;*****************************************************************************